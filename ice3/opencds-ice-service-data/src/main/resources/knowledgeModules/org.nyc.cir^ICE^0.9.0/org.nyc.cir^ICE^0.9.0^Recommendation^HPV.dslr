/**
 * Copyright (C) 2015 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.nyc.cir.ICE_v1_0_0

import java.util.List
import java.util.Set
import java.util.Date
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.Recommendation.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdata.tmp.SupportedDiseaseConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedEvaluationConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedPersonConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedRecommendationConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedVaccineConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedVaccineGroupConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.service.TimePeriod
import org.cdsframework.ice.service.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander org.nyc.cir^ICE^0.9.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext

// If patient is male, >= 22 yrs and < 27 yrs and has no doses on record, series is conditionally recommended, reason = HIGH RISK
rule "HPV: If patient is male, >= 22 yrs and < 27 yrs and has no doses on record, series is conditionally recommended, reason = HIGH RISK" ruleflow-group "RecommendationForecast"
	salience 200
	agenda-group "customRecommendationRule"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- the Patient is Male
		There is a Series $targetSeries that needs Forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.HPV
			- the effective dose number in the Series is == 1
		Confirm elapsed time between $dtBirthDate and evalTime >= "22y" && elapsed time between $dtBirthDate and evalTime < "27y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to SupportedRecommendationConcept._RECOMMENDED_CONDITIONALLY_HIGH_RISK_REASON.getConcept()
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// The interval between dose 1 & 3 of HepB needs to be at least 112 days for Recommendation. Routine interval rule still runs
rule "HPV: Recommend an interval of 6m between doses 1 & 3" ruleflow-group "RecommendationForecast"
	salience 200
	agenda-group "customRecommendationRule"
	when
		There is an Administered Shot $dose1 
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID or ACCEPTED
			- Make note of the Date this Shot was Administered as $dtAdministrationDate
		There is a Series $targetSeries that needs Forecasting
			- the Series contains the Dose $dose1
			- the effective dose number in the Series is == 3
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.HPV
			- the Series is not Complete
	then
		Add 6 DurationType.MONTHS to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Include a Recommendation as $recommendation with Forecast Date $dtCalculated for consideration in the final Forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "HPV: Recommend NOT_RECOMMENDED TOO_OLD If Patient >=27 yrs and does not have one or more Doses <27 yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "27y" of age
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.HPV
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "27y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to SupportedRecommendationConcept._NOT_RECOMMENDED_TOO_OLD_REASON.getConcept()
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "HPV: Recommend Conditionally ABOVE_AGE_MAY_COMPLETE if Series Not Complete, Patient >=27 yrs and has one or more doses <27 yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "27y" of age
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.HPV
			- the Series is Not Complete
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is > 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "27y"	
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to SupportedRecommendationConcept._CONDITIONAL_RECOMMENDATION_ABOVE_AGE_MAY_COMPLETE_REASON.getConcept()
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

