/**
 * Copyright (C) 2015 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.nyc.cir.ICE_v1_0_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.IceFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.Recommendation.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdata.tmp.SupportedDiseaseConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedFactConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedEvaluationConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedRecommendationConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedVaccineConcept
import org.cdsframework.ice.supportingdata.tmp.SupportedVaccineGroupConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.service.TimePeriod
import org.cdsframework.ice.service.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander org.nyc.cir^ICE^0.9.0.dsl

//declare any global variables here
global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


rule "Meningococcal: Mark the Series as Complete Before Evaluation of Shot if Prior Dose 1 is Administered >= 16yrs and < 19yrs of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "postEvaluationCheck"
	when
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAge when the patient is "16y" of age				
		There is an Administered Shot $dose1
			- the Dose Number in the Series is == 1
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the date this shot was administered as $dtAdministrationDateOne
		There is a Series $targetSeries that contains the Shot $dose1
			- the Series belongs to the vaccine group SupportedVaccineGroupConcept.Meningococcal
			- the name of the Series is "MCV4 2-Dose Series"
			- the Series is Not Complete
			- the number of Doses Administered is == 1
			- the number of Doses Administered before $dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDateOne >= "16y" && elapsed time between $dtBirthDate and $dtAdministrationDateOne < "19y"			
	then
		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Meningococcal: Evaluate shot as Accepted/Below Recommended Age, does not count towards series completion if given below the minimum age for the series and above the vaccine minimum age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the dose number in the series is == 1
			- make note of the date this shot was administered as $shotDate
			- make note of the minimum vaccine age for this shot as $validMinAgeVaccine
		There is a Series $targetSeries that contains the Shot $currentShot
			- the Series belongs to the vaccine group SupportedVaccineGroupConcept.Meningococcal
			- make note of the Absolute Minimum Age for Dose 1 in this Series as $minAgeDoseOne
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $shotDate < $minAgeDoseOne && elapsed time between $dtBirthDate and $shotDate >= $validMinAgeVaccine 
	then
		Mark that the Shot $currentShot does not count towards Completion of the Series
		Mark the shot $currentShot as Accepted for this Series due to "Below Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Meningococcal: Skip Evaluation of the Shot if given below the minimum age for the series and below the vaccine minimum age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- make note of the date this shot was administered as $shotDate
			- make note of the minimum vaccine age for this shot as $validMinAgeVaccine
		There is a Series $targetSeries that contains the Shot $currentShot
			- the Series belongs to the vaccine group SupportedVaccineGroupConcept.Meningococcal
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $shotDate < $validMinAgeVaccine 
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Evaluate Shot as Accepted & Above Recommended Age if age at administration >= 19yrs and has not completed series < 19yrs of age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
    when
		There is an Administered Shot $currentShot that needs to be evaluated
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "19y" of age
		There is a Series $targetSeries that contains the Shot $currentShot
			- the Series belongs to the vaccine group SupportedVaccineGroupConcept.Meningococcal
			- the Series is not Complete
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDateCurrentDose >= "19y"
    then
		Mark the shot $currentShot as Accepted for this Series due to "Above Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Mark that the Shot Administered >= 19yrs of age does not count towards completion of the series" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- make note of the date this shot was administered as $dtAdministrationDateCurrentDose
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries that contains the Shot $currentShot
			- the Series belongs to the vaccine group SupportedVaccineGroupConcept.Meningococcal
		Confirm elapsed time between $dtBirthDate and $dtAdministrationDateCurrentDose >= "19y"
	then
		Mark that the Shot $currentShot does not count towards Completion of the Series
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

