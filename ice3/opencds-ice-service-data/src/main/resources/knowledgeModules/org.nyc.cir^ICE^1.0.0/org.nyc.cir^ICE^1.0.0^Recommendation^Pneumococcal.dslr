/**
 * Copyright (C) 2017 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */
 
package org.cdsframework.ice.v1_1_0

import java.util.Date
import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl 

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext
global org.cdsframework.ice.service.Schedule schedule



/*************************************************************************************************************************************************************************************
Rules for Recommending at the CVX Code vs. Vaccine Group Level

* When recommending in the Child Series, always recommend at the CVX level; vaccine recommended should be PCV 13 (CVX 133).
* When recommending in the Adult Series, 
    + If the patient is < 65 years old, then recommend at the vaccine group level.
    + If the patient is >= 65 years old, then always recommend at the CVX level:
        - Recommend PCV 13 (CVX 133) for dose 1. 
        - If dose 1 was a PCV (CVX 133, 152), then PPSV23 (CVX 33) should be recommended for dose 2.
        - If dose 1 was a PPSV (CVX 33, 109), then PCV13 (CVX 133) should be recommended for dose 2.
/************************************************************************************************************************************************************************************/

// Pneumococcal Child Series: When recommending in the Child Series, always recommend at the CVX level; vaccine recommended should be PCV 13 (CVX 133).
rule "Pneumococcal Child Series: Recommend at the vaccine level in child series; vaccine recommended should be PCV 13 (CVX 133)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the Series is not complete
			- the name of the Series is "PneumococcalChildSeries"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommended Vaccine for the Forecast in the Series $targetSeries to "ICE133"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Pneumococcal Adult Series: If the patient is < 65 years old, then recommend at the vaccine group level
// This is the default behavior - no custom rule needed


// Pneumococcal Adult Series: If the patient is >= 65 years old, then always recommend for target dose 1 vaccine PCV 13 (CVX 133)
rule "Pneumococcal Adult Series: Recommend PCV 13 (CVX 133) for target dose 1 if patient >= 65 years of age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalAdultSeries"
			- the effective dose number in the Series is == 1
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "65y"			
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommended Vaccine for the Forecast in the Series $targetSeries to "ICE133"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Pneumococcal Adult Series: If the patient is >= 65 years old, then always recommend a vaccine
// - If dose 1 was a PCV (CVX 133, 152), then PPSV23 (CVX 33) should be recommended for dose 2.
rule "Pneumococcal Adult Series: If the patient is >= 65 years old, always recommend PPSV23 (CVX 33) for target dose 2 if dose 1 was PCV (CVX 133, 152)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalAdultSeries"
			- the effective dose number in the Series is == 2
		There is an administered shot $currentShot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == 1
			- the vaccine administered a member of ("ICE133", "ICE152")
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "65y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommended Vaccine for the Forecast in the Series $targetSeries to "ICE33"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Pneumococcal Adult Series: If the patient is >= 65 years old, then always recommend a vaccine
// - If dose 1 was a PPSV (CVX 33, 109), then PCV13 (CVX 133) should be recommended for dose 2.
rule "Pneumococcal Adult Series: If the patient is >= 65 years old, always recommend PCV13 (CVX 133) for target dose 2 if dose 1 was PPSV (CVX 33, 109)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalAdultSeries"
			- the effective dose number in the Series is == 2
		There is an administered shot $currentShot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == 1
			- the vaccine administered a member of ("ICE33", "ICE109")
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "65y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommended Vaccine for the Forecast in the Series $targetSeries to "ICE133"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 END Rules for Recommending at the CVX Code vs. Vaccine Group Level
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Recommendation Rules based on Age
* If a patient is < 5 years and has completed the Child series (including meeting the PCV 13 Rule), then the Recommendation is Not Recommended and the reason code is COMPLETE_HIGH_RISK.  
* If a patient is >= 5 years (or will be >= 5 years at the recommended due date) and < 65 years, then the Recommendation is Conditional and the reason code is HIGH_RISK.
/************************************************************************************************************************************************************************************/

// If patient is < 5 years and has completed the Child series (including meeting the PCV 13 Rule), then the Recommendation is Not Recommended and the reason code is COMPLETE_HIGH_RISK.  
rule "Pneumococcal Child Series: if patient < 5yrs completed the Child series (including the PCV13 rule), recommend Not Recommended with reason COMPLETE_HIGH_RISK" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 210
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- the Series is Complete
		There is an administered shot $shot
			- the shot belongs to the Series $targetSeries
			- the vaccine administered is "ICE133"
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE_HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If patient is >= 5 years and < 65 years, then the Recommendation is Conditional and the reason code is HIGH_RISK.
rule "Pneumococcal Series: if patient *is* >= 5yrs and < 65yrs of age, then recommend Conditional High Risk" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y" && elapsed time between $dtBirthDate and evalTime < "65y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Post Recommendation Check: If patient will be >= 5 years at the recommended due date and < 65 years at the recommended due date, then the Recommendation is Conditional and  
// the reason code is HIGH_RISK. (Rule "Pneumococcal Series: Recommend Conditional High Risk if Patient Age >=5yrs and < 65yrs of Age" already checks if patient age is >= 5yrs 
// and < 65yrs. This rule will only fire if there is a recommendation date attached to the final recommendation (i.e. - if the recommendation status is RECOMMENDED or RECOMMENDED_IN_FUTURE)
// and with the added conditional recommendation, ICE will override that recommendation with the conditionally recommended (as per general ICE rules)
rule "Pneumococcal: if patient *will be* >= 5 years at the recommended due date and < 65 years, then recommend Conditional and HIGH_RISK" ruleflow-group "RecommendationForecast"
	agenda-group "postRecommendationCheck"
	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- make note of the Recommendation Date as $recommendationDate
			- post processing on the Series forecast has not already been run
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
		Confirm elapsed time between $birthDate and $recommendationDate >= "5y" && elapsed time between $birthDate and $recommendationDate < "65y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.CONDITIONALLY_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Mark the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Recommended Interval: if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV in the child series. 
// For the purpose of recommendation, ignore the PPSV23 shot.
rule "Pneumococcal: if the patient < 2yrs, if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- make note of the Last Shot Administered in the Series as $lastAdministeredShot
		There is an administered shot $shot
			- that is the same shot as $lastAdministeredShot
			- the vaccine administered is "ICE33"
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime < "2y"
	then
		Mark the shot $shot as Ignored
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Recommended Interval: if a PPSV23 (CVX 33) shot has been given, the recommended interval is 0 days from the PPSV23 to the next target dose of PCV in the child series. 
// For the purpose of recommendation, ignore the PPSV23 shot.
rule "Pneumococcal: if the patient >= 2yrs and < 5 years, if a PPSV23 (CVX 33) shot has been given, the recommended interval is 56 days from the PPSV23 to the next target dose of PCV" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- make note of the Last Shot Administered in the Series as $lastAdministeredShot
		There is an administered shot $shot
			- that is the same shot as $lastAdministeredShot
			- the vaccine administered is "ICE33"
			- Make note of the date this shot was administered as $dtAdministrationDate
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime >= "2y" && elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Add "56d" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Include a Recommendation as $r with Forecast Date $dtCalculated for consideration in the final Forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
 END Recommendation Rules based on Age
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
PCV 13 Rule: If a child is complete for the series, has not had a dose of PCV 13 (CVX 133), is currently < 5 years old, and will be < 5 years old at the recommended due date, 
then a dose of PCV 13 (CVX 133) is recommended. Recommended interval is 8 weeks
/************************************************************************************************************************************************************************************/

// If a child is complete for the series, has had one or more doses but not had a dose of PCV 13 (CVX 133), is currently < 5 yrs old and will be < 5 years old at the recommended due date, 
// then a dose of PCV 13 (CVX 133) is recommended. Recommended interval = 8 weeks from the previous shot.
rule "Pneumococcal Child Series: Recommend dose if series complete if no prior VALID PCV13 doses, is <5 yrs old and will be less than 5yrs old upon recommendation date" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 210
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- the Series is Complete
			- the Number of Administered Shots is > 0
			- make note of the date that the most recent shot was administered as $shotDate			
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine "ICE133" is == 0
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and evalTime < "5y"
		Confirm elapsed time between $dtBirthDate and (calculate date from addition of $shotDate with TimePeriod "8w") < "5y"
	then
		Add "8w" to $shotDate and make note of the newly calculated date as $dtCalculated
		Include a recommendation as $recommendation with forecast date $dtCalculated for consideration in the final forecast of the series $targetSeries
		Mark forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END PCV 13 Rule
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Pneumococcal Child Series 4-dose Series Exception Rules
/************************************************************************************************************************************************************************************/

// Exception 1
rule "Pneumococcal Child Series: Include Recommendation with Date 7months in 4-Dose PCV Series and TargetDose 2 if Patient is between 7and12months and Received No Doses before 7months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeSevenMonths when the Patient is "7m" of age
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- the effective dose number in the Series is <= 2
			- the Number of Doses Administered before $dtDateAtAgeSevenMonths is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Include a Recommendation as $recommendation with Forecast Date $dtDateAtAgeSevenMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 2 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 2A
rule "Pneumococcal Child Series: Include Recommendation with Date 12months and TargetDose 3 in 4-Dose PCV Series if Patient between age 12and24months and Received <2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age	
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- the effective dose number in the Series is <= 3
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is < 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"
	then
		Include a Recommendation as $recommendation with Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 3 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 2B
rule "Pneumococcal Child Series: Set Recommendation Date to 12months and TargetDose 4 in 4-Dose PCV Series and Patient between age 12and24months and Received 2 Doses before 12months of Age" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	activation-group "recommendationAgeCheck"
	when
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwelveMonths when the Patient is "12m" of age
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- the effective dose number in the Series is <= 4
			- the Number of Doses Administered before $dtDateAtAgeTwelveMonths is == 2
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"
	then
		Include a Recommendation as $recommendation with Forecast Date $dtDateAtAgeTwelveMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


// Exception 3
rule "Pneumococcal Child Series: Set Recommendation Date to 24months and TargetDose 4 in 4-Dose PCV Series if Patient between age 24months and 5years" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	salience 200
	when
		The patient information $assign_oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dtDateAtAgeTwentyFourMonths when the Patient is "24m" of age					
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalChildSeries"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the effective dose number in the Series is <= 4
			- the Effective Number of Doses in the Series before $dtDateAtAgeTwentyFourMonths is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and evalTime >= "24m" && elapsed time between $dtBirthDate and evalTime < "5y"
	then
		Include a Recommendation as $recommendation with Forecast Date $dtDateAtAgeTwentyFourMonths for Consideration in the final Forecast of the Series $targetSeries
		Set the Dose Number of Recommendation Forecast to 4 in Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
 END Pneumococcal Child Series 4-dose Series Exception Rules
/************************************************************************************************************************************************************************************/


