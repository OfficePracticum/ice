/**
 * Copyright (C) 2017 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.0.0/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


rule "Pneumococcal Adult Series: Evaluate PSV Shot >= 5yrs and < 19yrs as Accepted with reason code BELOW_REC_AGE_SERIES" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentDose that needs to be evaluated
			- the dose number in the Series is >= 5
			- the vaccine administered a member of ("ICE133", "ICE152")
			- make note of the date this shot was Administered as $administrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate >= "5y"
		Confirm elapsed time between $dtBirthDate and $administrationDate < "19y"
	then
		Mark the shot $currentDose as Accepted for this Series due to "Below Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "Pneumococcal Adult Series: Evaluate PPSV Shot (CVX 33, 109) >= 5yrs and < 65y-4d as Accepted with reason code BELOW_REC_AGE_SERIES" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		There is an Administered Shot $currentShotForAgeCheck that needs to be evaluated
			- the dose number in the Series is >= 5
			- the vaccine administered a member of ("ICE33", "ICE109")
			- make note of the date this shot was Administered as $administrationDate
		There is a Series $targetSeriesForAgeCheck that contains the Dose $currentShotForAgeCheck
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate >= "5y"
		Confirm elapsed time between $dtBirthDate and $administrationDate < "65y-4d"			
	then
		Mark the shot $currentShotForAgeCheck as Accepted for this Series due to "Below Recommended Age"
		Record that this dose rule was processed for the TargetDose $currentShotForAgeCheck
		Log that this dose rule fired for the dose $currentShotForAgeCheck in the Series $targetSeriesForAgeCheck
end


// Adult Series: Minimum interval from the accepted PPSV to PPSV (target dose in the adult series) = 5 years
rule "Pneumococcal Adult Series: Minimum interval from the accepted PPSV to PPSV (target dose in the adult series) = 5 years" 
		extends "Pneumococcal Adult Series: Evaluate PPSV Shot (CVX 33, 109) >= 5yrs and < 65y-4d as Accepted with reason code BELOW_REC_AGE_SERIES"
	dialect "mvel"		
	ruleflow-group "HistoryEvaluation"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $currentShotForIntervalCheck that needs to be evaluated
			- the dose number in the Series is >= 5
			- that is the same shot as $currentShotForAgeCheck
			- make note of the administered shot number as $nShotNumber
		There is an Administered Shot $previousShot
			- the administered shot number is == $nShotNumber-1
			- the vaccine administered a member of ("ICE33", "ICE109")
			- the dose number in the Series is == 5
			- make note of the date this shot was administered as $dtAdministrationDatePreviousShot
		There is a Series $targetSeriesForIntervalCheck
			- that is the same series as $targetSeriesForAgeCheck
			- the Series also contains the shot $previousShot
		Confirm Elapsed time between $dtAdministrationDatePreviousShot and $administrationDate < "5y"	
	then
		Mark the shot $currentShotForIntervalCheck as Invalid for this Series due to "Below Minimum Interval"
		Record that this dose rule was processed for the TargetDose $currentShotForIntervalCheck
		Log that this dose rule fired for the dose $currentShotForIntervalCheck in the Series $targetSeriesForIntervalCheck
end


/////// Adult Series: Minimum interval from the accepted PPSV to PCV (target dose in the adult series) = 0 days
/////// (This rule does not need to be implemented because the default absolute minimum interval from dose 1 for the Adult Series is 0 days, 
/////// as per the current supporting data 
///////


/////////////////////
// Adult Series Rules for CVX 100 (PCV 7)
//     1) If CVX 100 (PCV 7) is reported, then evaluate as Accepted and the reason code is VACCINE_NOT_ALLOWED. 
//     2) Ignore the reported CVX 100 when determining the recommended vaccine and calculating the recommended due date for the next target dose.
// Adult Series: If PCV 7 is administered in the Child Series, then mark the shot as *Accepted* (rather than INVALID) with a reason code of VACCINE_NOT_ALLOWED_FOR_THIS_DOSE.
//
// Adult Series: PCV 7 Rule #1
rule "Pneumococcal Adult Series: Evaluate PCV 7 shot (CVX 100) as Accepted with a reason code of VACCINE_NOT_ALLOWED" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the vaccine administered is "ICE100"
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the Series is >= 5
	then
		Mark the shot $currentShot as Accepted for this Series due to "Vaccine Not Allowed"
		Mark that evaluation of shot $currentShot is complete and therefore should not be reevaluated by any other rules
		Record that this dose rule was processed for the TargetDose $currentShot
end


// Adult Series: PCV 7 Rule #2
rule "Pneumococcal Adult Series: Note that the Absolute Minimum interval from PCV 7 = 0d by marking the shot Ignored" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 10
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the vaccine administered is "ICE100"
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- the dose number in the Series is >= 5
	then
		Mark the shot $currentShot as Ignored
		Record that this dose rule was processed for the TargetDose $currentShot
end

////////////////////
// Adult Series: If PCV (CVX 133 or 152) is administered for dose 5 (1st dose of adult series), then the absolute minimum age and minimum age is 19 years. 
// Otherwise, the vaccine administered is a CVX (33, 109) and should have absolute minimum age and minimum age are 65y-4d and 65y, respectively, as per the series definition.
rule "Pneumococcal Adult Series: If PCV (CVX 133, 152) administered for dose 5, modify DoseRule5 absolute minimum and minimum age to 19y" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	no-loop true
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the dose number in the series is == 5
			- the vaccine administered a member of ("ICE133", "ICE152")
		There is a Series $targetSeries that contains the shot $currentShot
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
	then
		Obtain the existing DoseRule for Dose Number 5 in the Series $targetSeries as $assign_oDoseRule5
		Set the Absolute Minimum Age for DoseRule $assign_oDoseRule5 to TimePeriod "19y"
		Set the Minimum Age for DoseRule $assign_oDoseRule5 to TimePeriod "19y"
		Replace the existing DoseRule in the Series $targetSeries with $assign_oDoseRule5
		Refresh all Facts in the Series $targetSeries for Evaluation
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

/////////////////////
// Child Series: If an invalid vaccine is administered in the Child Series, mark the shot as *Accepted* (rather than INVALID) with a reason code of VACCINE_NOT_ALLOWED_FOR_THIS_DOSE. 
// As of 10/2016, only CVX 33. (Overrides general "allowableVaccineCheck" rule)
rule "Pneumococcal Child Series: Evaluate shot with invalid vaccine as Accepted with reason code of VACCINE_NOT_ALLOWED_FOR_THIS_DOSE" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750" and the Series with name "PneumococcalSeries"
			- make note of the administered vaccine as $vaccineAdministered
			- make note of the dose number as $doseNumber
		There is a Series $targetSeries that contains the shot $currentShot
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is <= 4
			- the vaccine $vaccineAdministered is not permitted for dose number $doseNumber in this series
	then
		Mark the shot $currentShot as Accepted for this Series due to "Vaccine Not Allowed For This Dose"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// Child Series: Minimum Interval the minimum interval between a PPSV23 (CVX 33) in Child Series and a dose of PCV (CVX 100, 133, 152, 109) is 0 days.
// (Since the PPSV shot is invalid, simply mark it as ignored)
rule "Pneumococcal Child Series: Evaluate the minimum interval as 0 days between a PPSV23 (CVX 33) and a shot of PCV (CVX 100, 133, 152, 109) administered at < 5yrs of age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the effective dose number in the series is <= 4
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the series $targetSeries
			- the administered shot number is > 1
			- the vaccine administered a member of ("ICE100", "ICE133", "ICE152", "ICE109")
			- make note of the administered shot number as $nShotNumber
			- make note of the date this shot was administered as $administrationDate
		There is an administered shot $previousShot
			- the shot belongs to the series $targetSeries
			- the administered shot number is <= $nShotNumber-1
			- the vaccine administered is "ICE33"
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y"
	then
		Mark the shot $previousShot as Ignored
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// Abstract rule for PCV13 rule: patient < 5yrs of age, series is complete and no prior PCV shots until the current shot under evaluation
rule "Pneumococcal Child Series (Abstract Rule): Evaluate PCV Shot for patient < 5yrs, series is complete and no prior PCV shots" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Series is Complete
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the series $targetSeries
			- the vaccine administered is "ICE133"
			- make note of the administered shot number as $nShotNumber
			- make note of the Date this Shot was Administered as $administrationDate
		There is an administered shot $previousShot
			- the shot belongs to the series $targetSeries
			- the administered shot number is == $nShotNumber-1
			- make note of the Date this Shot was Administered as $previousAdministrationDate
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine "ICE133" is == 0
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y"
	then
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// If a child is already complete for the series, has not had a dose of PCV 13 (CVX 133), is currently < 5 yrs old in the Child Series, then this PCV 13 shot 
// is evaluated to VALID as long as an interval of 52 days has elapsed since last shot.
rule "Pneumococcal Child Series: Evaluate PCV Shot as Valid if Patient < 5yrs, interval is >= 52 days and Series is Complete" extends 
		"Pneumococcal Child Series (Abstract Rule): Evaluate PCV Shot for patient < 5yrs, series is complete and no prior PCV shots" 
	ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		Confirm elapsed time between $previousAdministrationDate and $administrationDate >= "52d"
	then
		Mark the shot $currentShot as Valid for this Series
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// If a child is already complete for the series, and this shot is PCV 13 (CVX 133), is currently < 5 yrs old, then this PCV 13 shot is evaluated to INVALID if an interval 
// of 52 days has not elapsed since last shot.
rule "Pneumococcal Child Series: Evaluate PCV Shot as Invalid if Patient < 5yrs, interval is < 52 days and Series is Complete" extends 
	"Pneumococcal Child Series (Abstract Rule): Evaluate PCV Shot for patient < 5yrs, series is complete and no prior PCV shots" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		Confirm elapsed time between $previousAdministrationDate and $administrationDate < "52d"
	then
		Mark the shot $currentShot as Invalid for this Series due to "Below Minimum Interval"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 1: Evaluate PCV Shot as Below Minimum Age for Final Dose if 4th Dose Administered < 361 Days of Age and Received 0 Doses Administered Prior to 7m of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "7m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- the Dose Number in the Series is == 4			
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is == 0			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "361d"	
	then
		Mark the shot $currentDose as Invalid for this Series due to "Below Minimum Age for Final Dose"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 1: Set Series Dose Number to 2 if No Shots Administered and Patient >= 7m and < 12 Months at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $assign_dtDateAtAge when the patient is "7m" of age
		There is a Series $targetSeries
			- the Number of Administered Shots is == 0
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 2 from 1 for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "Pneumococcal Child Series: Exception 1: Skip Dose Number to 2 for Patient Between >= 7m and < 12m at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 2
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m"
	then
		Set Dose Number of $currentDose to 2
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 2 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 2: Set Series Dose Number to 3 if No Shots Administered and Patient >= 12m and < 24m of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"	
	then
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 3 from 1 for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 2: Skip Dose Number to 3 if Patient Between >= 12m and < 24m at Shot Date and has Received <2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 3
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is < 2			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 3
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 3 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 2: Skip Dose Number to 4 if Patient Between >= 12m and < 24m and has Received 2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Doses Administered before $dateOfInterest is == 2
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 4
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 3: Set Series Dose Number to 4 if No Shots Administered and Patient >= 24m and < 5y of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "24m" && elapsed time between $dtBirthDate and evalTime < "5y"	
	then
		// Pneumoccocal pneumonia
		Skip Series Dose Number to 4 from 1 for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 3: Skip Dose Number to 4 for Patient >= 24m at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "24m"
	then
		Set Dose Number of $currentDose to 4
		// Pneumococcal pneumonia
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Adult Series: Exception 4: Set Series Dose Number to 5 if No Shots Administered and Patient >= 5y of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
			- the Number of Administered Shots is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "5y"
	then
		/////// Mark the series as not complete, since the childhood portion of this series may have been marked complete
		Skip Series Dose Number to 5 from 1 for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Pneumococcal Child Series: Exception 4: Skip Dose Number to 5 for Patient >= 5y at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 5
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.750"
			- the name of the Series is "PneumococcalSeries"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "24m"
	then
		// Mark the series as not complete, since the childhood portion of this series may have been marked complete
		/////// Mark the Series $targetSeries Not Complete
		Set Dose Number of $currentDose to 5
		Skip Series Dose Number to 5 from $assign_nDoseNumber for Disease "SUPPORTED_DISEASE_CONCEPT.481" in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end
