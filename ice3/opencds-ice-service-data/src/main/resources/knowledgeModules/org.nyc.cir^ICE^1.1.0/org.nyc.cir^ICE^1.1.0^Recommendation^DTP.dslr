/**
 * Copyright (C) 2015 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0 

import java.util.Date
import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.Recommendation.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedDiseaseConcept
import org.cdsframework.ice.supportingdatatmp.SupportedEvaluationConcept
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.supportingdatatmp.SupportedVaccineConcept
import org.cdsframework.ice.supportingdatatmp.SupportedVaccineGroupConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.1.0/org.cdsframework^ICE^1.1.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


/*************************************************************************************************************************************************************************************
// Generally, ICE recommends at the group level rather than a specific CVX code; however, DTP vaccine group recommendations are at the CVX level.
// When recommending DTP as part of the 3-dose or 5-dose series, the rules below state which CVX code to recommend. For patients >=7 years of age, 
// the rules below also include an override to the series routine recommended age.
// If the patient is < 7 years, always recommend DTaP NOS (CVX 107).
// If a patient is >=7 years and
//     * Has received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Td (CVX 09).
//     * Recommended Age Override (overrides series table routine recommended age for next dose) :
//         + Recommended Age = 7 years
//         + Recommended interval follows table rules
// If patient is >= 7yrs and has NOT received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Tdap (CVX 115).
//     * Recommended Age Override (overrides series table routine recommended age for next dose): 
//         + Recommended Age = 7 years
//         + Recommended interval follows table rules
*************************************************************************************************************************************************************************************/

// If the patient is < 7 years, always recommend DTaP NOS (CVX 107).
rule "DTP: Recommend at the vaccine level in DTP series; if the patient < 7yrs and series is not complete, always recommend DTaP NOS (CVX 107)" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is not complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
		The Immunization Schedule information $schedule must be known to complete writing this rule
		Confirm elapsed time between $dtBirthdate and evalTime < "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation of Schedule $schedule to SupportedVaccineConcept._DTAP_UNSPECIFIED
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If a patient is >=7 years; has _not_ completed the series and has received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Td (CVX 09).
rule "DTP: Recommend Td (CVX 09) at 7yrs of age if series is not complete, patient >= 7yrs, has received a dose of pertussis >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	when
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is not complete
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the series $targetSeries as well as IceResult administration date >= $dtDateAtAge
		The Immunization Schedule information $schedule must be known to complete writing this rule
		Confirm elapsed time between $dtBirthdate and evalTime >= "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation of Schedule $schedule to SupportedVaccineConcept._TD_ABSORBED
		Set the Recommendation Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If a patient is >=7 years, has _not_ completed the series and has _not_ received a dose of pertussis at >=7 years of age, then the next dose in the series should be Tdap (CVX 115).
rule "DTP: Recommend Tdap (CVX 115) at age 7yrs if series is not complete, if patient >= 7yrs and has _not_ received a dose of Pertussis >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"			// override series default age rule
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is not complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		There is not an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the series $targetSeries as well as IceResult administration date >= $dtDateAtAge
		The Immunization Schedule information $schedule must be known to complete writing this rule
		Confirm elapsed time between $dtBirthdate and evalTime >= "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation of Schedule $schedule to SupportedVaccineConcept._TDAP
		Set the Recommendation Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
General Vaccine Recommendation rules END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
For patients who completed the 5-dose series:
* Recommended Vaccine = Tdap (CVX 115) 
* Recommended Interval = 0 days (from dose 5 or an accepted/extra dose, if one exists) and Recommended Age = 11 years unless an exception applies.  

If any one of the following exceptions apply, then: 
    * Recommended Age = 7 years
    * Recommended Interval = 6 months from the last dose of pertussis; 0 days from the last non-pertussis dose. 
        + Exception A) The patient DOES NOT have a dose of pertussis at >= 4 years. 
        + Exception B) The patient received dose 1 of the 5-dose series at < 12 months and DOES NOT have at least 4 doses of pertussis given at < 7 years
        + Exception C) The patient received dose 1 of the 5-dose series at >=12 months and DOES NOT have at least 3 doses of pertussis given at < 7 years
/************************************************************************************************************************************************************************************/

// For patients who completed the 5-dose series and no adolescent Tdap exceptions apply:
// * Recommended Vaccine = Tdap (CVX 115) 
// * Recommended Interval = 0 days (from dose 5 or an accepted/extra dose, if one exists) and Recommended Age = 11 years  
rule "DTP: Recommend Tdap at 11yrs of age & 0 day interval from last dose for completed 5-dose series if Adolescent Tdap 5-dose series recommendation exception did _not_ occur" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		// Get the most recent shot in the series
		There is an administered shot $administeredShot
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $dtAdministrationDate
			- the shot belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP and the Series with name "DTP 5-Dose Series"
			- make note of the associated series as $administeredShotSeries
		There does not exist another administered shot
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- the administration date of the shot is > $dtAdministrationDate
			- the shot belongs to the series $administeredShotSeries
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is complete
			- the name of the series is "DTP 5-Dose Series"
			- the series contains the shot $administeredShot
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There is not an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "11y" of age
		The Immunization Schedule information $schedule must be known to complete writing this rule
	then
		// Recommendation at age of 11yrs
		Create a Recommendation as $oRecommendationAge for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Vaccine for $oRecommendationAge of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendationAge for Consideration in the final forecast of the Series
		// Recommendation at 0 days from the last shot in the series
		Add 0 DurationType.DAYS to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If an Adolescent Tdap needed and Adolescent Tdap exception occurred, then: 
//    * Recommended Age = 7 years
rule "DTP: (Tdap 5-Dose Series Recommendation Exception Occurred)- Recommend Tdap at 7yrs for completed 5-dose series if Adolescent Tdap is needed an Adolescent Tdap 5-dose series recommendation exception occurred" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"	
	when
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the series is "DTP 5-Dose Series"
			- the Series is complete
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		The Immunization Schedule information $schedule must be known to complete writing this rule	
	then
		// Recommendation at age of 7yrs
		Create a Recommendation as $oRecommendationAge for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationAge to $dtDateAtAge
		Set the Recommendation Vaccine for $oRecommendationAge of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendationAge for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// If any one of the following exceptions apply, then: 
//    * Recommended Interval = 6 months from the last dose of pertussis; 0 days from the last non-pertussis dose. 
rule "DTP: (Tdap 5-Dose Series Recommendation Exception Occurred)- Recommend Tdap at 6 month interval from last pertussis dose for completed 5-dose series if an Adolescent Tdap is needed and an Adolescent Tdap 5-dose series recommendation exception occurred" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	when
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		There is an administered shot $administeredShotContainsPertussis
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $dtAdministrationDate
			- make note of the diseases targeted by the vaccine administered as $diseasesTargeted
			- the collection $diseasesTargeted contains SupportedDiseaseConcept.Pertussis
		There does not exist an administered shot
			- that is not the same shot as $administeredShotContainsPertussis
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedOther
			- the collection $diseasesTargetedOther contains SupportedDiseaseConcept.Pertussis
			- the administration date of the shot is > $dtAdministrationDate
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the series is "DTP 5-Dose Series"
			- the Series is complete
			- the series contains the shot $administeredShotContainsPertussis
		The Immunization Schedule information $schedule must be known to complete writing this rule
	then
		// Recommendation at 6 months from the last shot in the series
		Add 6 DurationType.MONTHS to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "DTP: (Tdap 5-Dose Series Recommendation Exception Occurred)- Recommend Tdap interval of 0 days from last non-pertussis dose for completed 5-dose series if Adolescent Tdap is needed and an Adolescent Tdap 5-dose series recommendation exception occurred" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	when
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		There exists an IceFact
			- that has finding SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.conceptCodeValue
		There is an administered shot $administeredShotContainsNonPertussis
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $dtAdministrationDate
			- make note of the diseases targeted by the vaccine administered as $diseasesTargeted
			- the collection $diseasesTargeted does not contain SupportedDiseaseConcept.Pertussis
		There does not exist an administered shot
			- that is not the same shot as $administeredShotContainsNonPertussis
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the diseases targeted by the vaccine administered as $diseasesTargetedOther
			- the collection $diseasesTargetedOther does not contain SupportedDiseaseConcept.Pertussis
			- the administration date of the shot is > $dtAdministrationDate
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the series is "DTP 5-Dose Series"
			- the Series is complete
			- the series contains the shot $administeredShotContainsNonPertussis
		The Immunization Schedule information $schedule must be known to complete writing this rule
	then
		// Recommendation at 0 days from the last non-pertussis dose in the series
		Add 0 DurationType.DAYS to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Exception A) The patient DOES NOT have a dose of pertussis at >= 4 years.
// Excepion A - higher activation priority than the general adolescent tdap recommendation rule above for 5-dose series 
rule "DTP: (Tdap 5-Dose Series Recommendation Exception A)- If patient does not have a dose of pertussis >= 4yrs in the 5-Dose Series(Exception A), make note that the Adolescent Tdap Recommendation Exception criteria was met" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is complete
			- the name of the series is "DTP 5-Dose Series"
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge when the patient is "4y" of age
		There is not an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the Series $targetSeries as well as IceResult administration date >= $dtDateAtAge
	then
		Logically Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.getConceptCodeValue() into working memory
		Log that this series rule fired for the Series $targetSeries
end


// Exception B)
// Exception B) The patient received dose 1 of the 5-dose series at < 12 months and DOES NOT have at least 4 doses of pertussis given at < 7 years
rule "DTP: (Tdap 5-Dose Series Recommendation Exception B)- If the patient received dose 1 of the 5-dose series at < 12 months and DOES NOT have at least 4 doses of pertussis given at < 7 years, make note that the Adolescent Tdap Recommendation Exception criteria was met" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is complete
			- the name of the series is "DTP 5-Dose Series"
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge12Months when the patient is "12m" of age
			- make note of the date as $dtDateAtAge7Years when the patient is "7y" of age
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the Series $targetSeries as well as IceResult administration date < $dtDateAtAge12Months
		Verify that the count of IceFacts (where IceResult finding == SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() as well as IceResult administration date < $dtDateAtAge7Years) is < 4
	then
		Logically Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.getConceptCodeValue() into working memory
		Log that this series rule fired for the Series $targetSeries
end

	
// Exception C)
// Exception C) The patient received dose 1 of the 5-dose series at >=12 months and DOES NOT have at least 3 doses of pertussis given at < 7 years
rule "DTP: (Tdap 5-Dose Series Recommendation Exception C)- If the patient received dose 1 of the 5-dose series at >= 12 months and DOES NOT have at least 3 doses of pertussis given at < 7 years, make note that the Adolescent Tdap Recommendation Exception criteria was met" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	dialect "mvel"
	salience 10
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is complete
			- the name of the series is "DTP 5-Dose Series"
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge12Months when the patient is "12m" of age
			- make note of the date as $dtDateAtAge7Years when the patient is "7y" of age
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the Series $targetSeries as well as IceResult administration date >= $dtDateAtAge12Months
		Verify that the count of IceFacts (where IceResult finding == SupportedFactConcept._DOSE_OF_PERTUSSIS.getConceptCodeValue() as well as IceResult administration date < $dtDateAtAge7Years) is < 3
	then
		Logically Insert an IceFact SupportedFactConcept._ADOLESCENT_TDAP_5_DOSE_SERIES_RECOMMENDATION_EXCEPTION.getConceptCodeValue() into working memory
		Log that this series rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
Adolescent 5-Dose Series Tdap rules END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
If the patient has completed the primary DTP series (3-dose or 5-dose) but has not received a dose of pertussis at >= 7 years, then an adolescent Tdap (CVX 115) is needed.

For patients who completed the 3-dose series (i.e., dose 1 at >=7  years):
    * Recommended Age = N/A
    * Recommended Interval = 0 days from dose 3
/************************************************************************************************************************************************************************************/


rule "DTP: If DTP 3-Dose Series is complete and Adolescent Tdap is required, recommend Tdap with age N/A and interval of 0 days from dose 3" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	when
		There is a series $targetSeries that needs forecasting
			- the series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the series is "DTP 3-Dose Series"
			- the series is complete
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_ADOLESCENT_TDAP_NEEDED.conceptCodeValue			
		The Immunization Schedule information $schedule must be known to complete writing this rule
	then
		Add "0d" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendation to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendation of Schedule $schedule to SupportedVaccineConcept._TDAP
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
Adolescent 3-Dose Series Tdap rules END
/************************************************************************************************************************************************************************************/



/*************************************************************************************************************************************************************************************
Rules for Recurring Td (CVX 09)
Once the patient has completed the primary DTP series (3-dose or 5-dose) and has received a dose of pertussis at >= 7 years of age, a Td (09) is needed every 10 years.  

Evaluation Rules for Recurring Td
    + All vaccines in the DTP vaccine group are allowed for the recurring Td.
    + Minimum interval = 0 days 
    + Once the primary series is complete and the patient has a dose of pertussis at >=7, any DTP shot is evaluated as VALID for the recurring Td.
*************************************************************************************************************************************************************************************/

// If a patient is >=7 years; has _not_ completed the series and has received a dose of pertussis at >=7 years of age, then the next dose in the series should be recommended as Td (CVX 09).
rule "DTP: Recommend Td (CVX 09) at interval of 10yrs if series is complete has received a pertussis dose >= 7yrs" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationIntervalCheck"
	when
		The Patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age			
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the Series is complete
			- make note of the date that the most recent shot was administered as $dtAdministrationDate
		There exists an IceFact
			- that has finding SupportedFactConcept._DOSE_OF_PERTUSSIS.conceptCodeValue
			- there is an associated administered shot in the Series $targetSeries as well as IceResult administration date >= $dtDateAtAge
		The Immunization Schedule information $schedule must be known to complete writing this rule
	then
		Add "10y" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationInterval to $dtCalculated
		Set the Recommendation Vaccine for $oRecommendationInterval of Schedule $schedule to SupportedVaccineConcept._TD_ABSORBED
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
Recurring Rules for Td END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
5-Dose Series Special Rules 
Rule for Recommending/Evaluating Next Dose From Invalid Tdap/TD Given Below Minimum Age for Vaccine
If Tdap (CVX 115) or Td (CVX 09, 113, 138, 139) is given as target dose 1, 2 or 3 to a patient <7 years - 4 days old, then the invalid shot should be ignored when determining the 
recommended due date for the next target dose due
*************************************************************************************************************************************************************************************/

// *AI*: Refactor this once general interval approach is refactored to get prior dose interval information
rule "DTP: Recommend recommended interval from prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients <= 7y-4d" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationIntervalCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the Series is "DTP 5-Dose Series"
			- make note of the effective dose number in the Series as $nDoseNumber
			- the numeric $nDoseNumber is <= 3
			- the Series is Not Complete
		// Get the most recent shot, only if it is a Td or Tdap that was invalid (and therefore has the same target dose number as the series)			
		There is an administered shot $currentShot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == $nDoseNumber
			- the vaccine administered a member of (SupportedVaccineConcept._TDAP.conceptCodeValue, SupportedVaccineConcept._TD_ABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_PRESERVATIVEFREE.conceptCodeValue, SupportedVaccineConcept._TD_NOTABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_NOS.conceptCodeValue)
			- make note of the administered shot number as $currentShotNumber
			- make note of the minimum vaccine age for this Shot as $strValidMinimumAge
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == $nDoseNumber
			- the administered shot number is > $currentShotNumber
		// Determine the shot from which to obtain the minimum interval DoseRule criteria (which occurred before the Td or Tdap shot)
		There is an Administered Shot $mostRecentShotForInterval
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is <= $nDoseNumber
			- the vaccine administered not a member of (SupportedVaccineConcept._TDAP.conceptCodeValue, SupportedVaccineConcept._TD_ABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_PRESERVATIVEFREE.conceptCodeValue, SupportedVaccineConcept._TD_NOTABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_NOS.conceptCodeValue)
			- make note of the Date this Shot was Administered as $administrationDate
			- make note of the administered shot number as $nAdministeredShotNumberForInterval
			- make note of the Earliest Recommended Interval from this dose to the next dose as $oTimePeriodRecommendationInterval
		There does not exist an Administered Shot
			- the shot belongs to the Series $targetSeries
			- the administered shot number is > $nAdministeredShotNumberForInterval
			- the vaccine administered not a member of (SupportedVaccineConcept._TDAP.conceptCodeValue, SupportedVaccineConcept._TD_ABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_PRESERVATIVEFREE.conceptCodeValue, SupportedVaccineConcept._TD_NOTABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_NOS.conceptCodeValue)
		The Patient information $evaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		// Check that Td or Tdap was admininstered at 7y-4d of age
		Confirm elapsed time between $birthdate and $administrationDate < $strValidMinimumAge
	then
		Add $oTimePeriodRecommendationInterval to $administrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $oRecommendationInterval for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendationInterval to $dtCalculated
		Include the Recommendation $oRecommendationInterval for Consideration in the final forecast of the Series
		Record that this dose rule was Processed for the TargetDose $mostRecentShotForInterval
		Log that this dose rule fired for the dose $mostRecentShotForInterval in the Series $targetSeries
end


rule "DTP: Recommend earliest interval from prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients <= 7y-4d" extends "DTP: Recommend recommended interval from prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients <= 7y-4d"
	ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "earliestIntervalCheck"
	when
		// Same as ancestor rule - for now, just don't do anything
	then
		Record that this dose rule was Processed for the TargetDose $mostRecentShotForInterval
		Log that this dose rule fired for the dose $mostRecentShotForInterval in the Series $targetSeries
end


// *AI*: Refactor this once general interval approach is refactored to get prior dose interval information
rule "DTP: NO recommended interval if no prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients < 7y-4d" ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "recommendationIntervalCheck"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the Series is "DTP 5-Dose Series"
			- make note of the effective dose number in the Series as $nDoseNumber
			- the numeric $nDoseNumber is <= 3
			- the Series is Not Complete
		// Get the most recent shot, only if it is a Td or Tdap that was invalid (and therefore has the same target dose number as the series)
		There is an administered shot $currentShot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == $nDoseNumber
			- make note of the administered shot number as $currentShotNumber
			- the vaccine administered a member of (SupportedVaccineConcept._TDAP.conceptCodeValue, SupportedVaccineConcept._TD_ABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_PRESERVATIVEFREE.conceptCodeValue, SupportedVaccineConcept._TD_NOTABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_NOS.conceptCodeValue)			
			- make note of the minimum vaccine age for this Shot as $strValidMinimumAge
			- make note of the date this shot was administered as $administrationDate
		There does not exist an administered shot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is == $nDoseNumber
			- the administered shot number is > $currentShotNumber
		// Check that there is no prior shot that is _not_ a Tdap or Td from which the interval can be based
		There does not exist an Administered Shot
			- the shot belongs to the Series $targetSeries
			- the dose number in the Series is <= $nDoseNumber
			- the vaccine administered not a member of (SupportedVaccineConcept._TDAP.conceptCodeValue, SupportedVaccineConcept._TD_ABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_PRESERVATIVEFREE.conceptCodeValue, SupportedVaccineConcept._TD_NOTABSORBED.conceptCodeValue, SupportedVaccineConcept._TD_NOS.conceptCodeValue)
		The Patient information $evaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		// Check that Td or Tdap was admininstered at 7y-4d of age
		Confirm elapsed time between $birthdate and $administrationDate < $strValidMinimumAge
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


rule "DTP: NO Recommended earliest interval from prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients < 7y-4d" extends "DTP: NO recommended interval if no prior vaccine containing D,T,& P in 5-dose series for target doses 1-3 for patients < 7y-4d"
	ruleflow-group "RecommendationForecast"
	dialect "mvel"
	agenda-group "customRecommendationRule"
	activation-group "earliestIntervalCheck"
	when
		// Same as ancestor rule - for now, just don't do anything
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
5-Dose Series Special Rules  END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
5-dose Series Exception Rules
The 5-dose series above applies unless:

Exception 1: Series Complete with 3 Doses
    * If the patient is >=7 or will be >=7 as of the next due date, received the first dose at >= 12 months of age, and received at least one dose at >= 4 years1 of age, then the 
      series is complete with 3 doses (doses 2, 3 and 4).
*************************************************************************************************************************************************************************************/

rule "DTP: (5-Dose Series Exception Rule 1)- Mark the 5-Dose Series Complete for Patient who will be >= 7yrs as of the next due date, if 3 Doses have been Administered with the first dose at >= 12m and at least 1 dose at >= 4yrs" 
 	ruleflow-group "RecommendationForecast"
 	agenda-group "postRecommendationCheck"
 	dialect "mvel"
 	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a Recommendation Date has been determined
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the series is "DTP 5-Dose Series"
			- the number of doses administered is == 3
			- make note of the Recommendation Date as $recommendationDate
			- post processing on the Series forecast has not already been run
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthDate
			- make note of the date as $dtDateAtAge4y when the patient is "4y" of age
			- make note of the date as $dtDateAtAge12m when the patient is "12m" of age
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge12m
			- the Dose Number in the series is == 1
		There exists an Administered Shot
			- the shot belongs to the Series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- the Administration Date of the shot is >= $dtDateAtAge4y
			- the Dose Number in the series is >= 2
		Confirm elapsed time between $birthDate and $recommendationDate >= "7y" 	
 	then
		Mark the Series $targetSeries Complete
		Refresh all Facts in the Series $targetSeries for forecasting
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

/*************************************************************************************************************************************************************************************
5-Dose Series Exception Rules  END
/************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Six by Seven Rule: If a child is < 7 years of age has received 6 shots from the DTP vaccine group, but has not yet completed the series, then the next dose is recommended at age 
7 years; follow table rules for to determine the recommended interval.
*************************************************************************************************************************************************************************************/

rule "DTP: (Six by Seven Rule)- Recommend next dose at age 7yrs for 5-dose series if patient < 7yrs, series is not complete and has received 6 shots" ruleflow-group "RecommendationForecast"
	agenda-group "customRecommendationRule"
	activation-group "recommendationAgeCheck"
	dialect "mvel"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.DTP
			- the name of the Series is "DTP 5-Dose Series"
			- the Series is not complete
			- the number of Administered Shots is >= 6
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
			- make note of the date as $dtDateAtAge when the patient is "7y" of age
		The Immunization Schedule information $schedule must be known to complete writing this rule
		Confirm elapsed time between $birthdate and evalTime < "7y"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Forecast Date for $oRecommendation to $dtDateAtAge
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

