/**
 * Copyright (C) 2016 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package org.cdsframework.ice.v1_1_0

import java.util.List
import java.util.Set
import org.drools.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedDiseaseConcept
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.supportingdatatmp.SupportedEvaluationConcept
import org.cdsframework.ice.supportingdatatmp.SupportedVaccineConcept
import org.cdsframework.ice.supportingdatatmp.SupportedVaccineGroupConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../org.cdsframework^ICE^1.1.0/org.cdsframework^ICE^1.1.0.dsl

global java.util.Date evalTime
global org.drools.runtime.KnowledgeContext kcontext


// FOR CHILDREN (<19), absolute minimum interval is 52 days between PPSV and PCV (regardless of order given); if given below 52 days, 
// invalid reason= BELOW_MIN_INTERVAL_PCV_PPSV
rule "PCV: Evaluate PCV Shot as Invalid if patient <19yrs and <absolute minimum interval 52days between PPSV and PCV not met" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- make note of the date this Shot was Administered as $administrationPCVDate
		There is an administered shot $ppsvShot distinct from $currentShot
			- make note of the date this Shot was Administered as $administrationPPSVDate
			- the administration date of the shot is <= $administrationPCVDate
		There is a Series $targetSeries that contains the shot $currentShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
		There is a Series $ppsvSeries that contains the shot $ppsvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PPSV
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationPPSVDate < "19y" && elapsed time between $administrationPCVDate and $administrationPPSVDate < "52d"
	then
		Mark the shot $currentShot as Invalid for this Series due to "Below Minimum Interval (PCV PPSV)"
		Record that this dose rule was processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// FOR ADULTS (>= 19) absolute minimum interval is 52 days between PCV and PPSV (in that order), invalid reason= BELOW_MIN_INTERVAL_PCV_PPSV 
 rule "PCV: Evaluate PCV Shot as Invalid if patient >=19yrs and <absolute minimum interval between PPSV & PCV in that order" ruleflow-group "HistoryEvaluation"
 	dialect "mvel"
 	agenda-group "customEvaluationRule"
 	when
 		There is an administered shot $pcvShot that needs to be evaluated
			- make note of the date this Shot was Administered as $administrationPCVDate
		There is an administered shot $ppsvShot distinct from $pcvShot
			- make note of the date this Shot was Administered as $administrationPPSVDate
			- the administration date of the Shot is <= $administrationPCVDate 				
		There is a Series $targetSeries that contains the shot $pcvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
		There is a Series $ppsvSeries that contains the shot $ppsvShot
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PPSV
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm elapsed time between $dtBirthDate and $administrationPPSVDate >= "19y" && elapsed time between $administrationPCVDate and $administrationPPSVDate < "361d" 
 	then
		Mark the shot $pcvShot as Invalid for this Series due to "Below Minimum Interval (PCV PPSV)"
		Record that this dose rule was processed for the TargetDose $pcvShot
		Log that this dose rule fired for the dose $pcvShot in the Series $targetSeries
end
 

// If a child is already complete for the series, has not had a dose of PCV 13 (CVX 133), is currently < 5 yrs old, then this PCV 13 shot 
// is evaluated to VALID as long as an interval of 52 days has elapsed since last shot.
rule "PCV: Evaluate PCV Shot as Valid if Patient < 5yrs, interval is >= 52 days and Series is Complete" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentDose that needs to be evaluated
			- the vaccine administered is SupportedVaccineConcept._PCV_13
			- the dose number in the series is > 4
			- make note of the administered shot number as $nShotNumber
			- make note of the Date this Shot was Administered as $administrationDate
		There is an administered shot $previousShot
			- the administered shot number is == $nShotNumber-1
			- make note of the Date this Shot was Administered as $previousAdministrationDate
		There is a Series $targetSeries that contains the shot $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Series also contains the shot $previousShot
			- the Series is Complete
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine SupportedVaccineConcept._PCV_13 is == 0
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y" && elapsed time between $previousAdministrationDate and $administrationDate >= "52d"					
	then
		Mark the shot $currentDose as Valid for this Series
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


// If a child is already complete for the series, and this shot is PCV 13 (CVX 133), is currently < 5 yrs old, then this PCV 13 shot 
// is evaluated to VALID as long as an interval of 52 days has elapsed since last shot.
rule "PCV: Evaluate PCV Shot as Invalid if Patient < 5yrs, interval is < 52 days and Series is Complete" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentDose that needs to be evaluated
			- the vaccine administered is SupportedVaccineConcept._PCV_13
			- the dose number in the series is > 4
			- make note of the administered shot number as $nShotNumber
			- make note of the Date this Shot was Administered as $administrationDate
		There is an administered shot $previousShot
			- the administered shot number is == $nShotNumber-1
			- make note of the Date this Shot was Administered as $previousAdministrationDate
		There is a Series $targetSeries that contains the shot $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Series also contains the shot $previousShot
			- the Series is Complete
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Verify that the Count of Doses Administered in Series $targetSeries with Vaccine SupportedVaccineConcept._PCV_13 is == 0
		Confirm elapsed time between $dtBirthDate and $administrationDate < "5y" && elapsed time between $previousAdministrationDate and $administrationDate < "52d"					
	then
		Mark the shot $currentDose as Invalid for this Series due to "Below Minimum Interval"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "PCV: Evaluate PCV Shot As Accepted If Patient >=5 Yrs and Series Not Complete by 5yrs of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "5y" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the target dose number for this shot as $assign_nDoseNumber
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"
	then
		Mark the shot $currentDose as Accepted for this Series due to "Above Recommended Age"
		Mark the Series $targetSeries Not Complete
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "PCV: Cancel Extra Dose Rule for PCV Shot if Patient >=5 Yrs and Series Not Complete by 5yrs of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the Date as $assign_dtDateAtAge when the Patient is "5y" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- make note of the target dose number for this shot as $assign_nDoseNumber
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- make note of the Number of Doses Required to Complete this Series as $assign_nNumberOfDosesRequired
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is < $assign_nNumberOfDosesRequired
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "5y"
	then
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end	


rule "PCV: Exception 1: Evaluate PCV Shot as Below Minimum Age for Final Dose of 4-Dose PCV Series if 4th Dose Administered < 361 Days of Age and Received 0 Doses Administered Prior to 7m of Age" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	activation-group "doseAgeCheck"
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "7m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
			- the Dose Number in the Series is == 4			
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Number of Doses Administered before $dateOfInterest is == 0			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "361d"	
	then
		Mark the shot $currentDose as Invalid for this Series due to "Below Minimum Age for Final Dose"
		Record that this dose rule was processed for the TargetDose $currentDose
		Log that this dose rule fired for the dose $currentDose in the Series $targetSeries
end


rule "PCV: Exception 1: Set Series Dose Number to 2 in 4-Dose PCV Series if No Shots Administered and Patient >= 7m and < 12 Months at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $assign_dtDateAtAge when the patient is "7m" of age
		There is a Series $targetSeries
			- the Number of Administered Shots is == 0
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Effective Number of Doses in the Series before $assign_dtDateAtAge is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "7m" && elapsed time between $dtBirthDate and evalTime < "12m"
	then
		Skip Series Dose Number to 2 from 1 for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "PCV: Exception 1: Skip Dose Number to 2 in 4-Dose PCV Series for Patient Between >= 7m and < 12m at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 2
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "7m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "12m"
	then
		Set Dose Number of $currentDose to 2
		Skip Series Dose Number to 2 from $assign_nDoseNumber for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "PCV: Exception 2: Set Series Dose Number to 3 in 4-Dose PCV Series if No Shots Administered and Patient >= 12m and < 24m of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Number of Administered Shots is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "12m" && elapsed time between $dtBirthDate and evalTime < "24m"	
	then
		Skip Series Dose Number to 3 from 1 for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "PCV: Exception 2: Skip Dose Number to 3 in 4-Dose PCV Series if Patient Between >= 12m and < 24m at Shot Date and has Received <2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 3
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Number of Doses Administered before $dateOfInterest is < 2			
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 3
		Skip Series Dose Number to 3 from $assign_nDoseNumber for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "PCV: Exception 2: Skip Dose Number to 4 in 4-Dose PCV Series if Patient Between >= 12m and < 24m and has Received 2 Doses Administered at <12m of Age at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
			- make note of the date as $dateOfInterest when the Patient is "12m" of age
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Number of Doses Administered before $dateOfInterest is == 2
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "12m" && elapsed time between $dtBirthDate and $assign_dtAdministrationDate < "24m"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "PCV: Exception 3: Set Series Dose Number to 4 in 4-Dose PCV Series if No Shots Administered and Patient >= 24m and < 5y of Age at Execution Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	no-loop true
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
			- the Number of Administered Shots is == 0
		Confirm elapsed time between $dtBirthDate and evalTime >= "24m" && elapsed time between $dtBirthDate and evalTime < "5y"	
	then
		Skip Series Dose Number to 4 from 1 for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "PCV: Exception 3: Skip Dose Number to 4 in 4-Dose PCV Series for Patient >= 24m at Shot Date" ruleflow-group "HistoryEvaluation"
	dialect "mvel"
	agenda-group "customEvaluationRule"
	salience 200
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		There is an Administered Shot $currentDose that needs to be Evaluated
			- the Dose Number in the Series is < 4
			- make note of the Dose Number for this Shot as $assign_nDoseNumber
			- make note of the Date this Shot was Administered as $assign_dtAdministrationDate
		There is a Series $targetSeries that contains the Dose $currentDose
			- the Series belongs to the Vaccine Group SupportedVaccineGroupConcept.PCV
			- the name of the Series is "PCV 4-Dose Series"
		Confirm elapsed time between $dtBirthDate and $assign_dtAdministrationDate >= "24m"
	then
		Set Dose Number of $currentDose to 4
		Skip Series Dose Number to 4 from $assign_nDoseNumber for Disease SupportedDiseaseConcept.Pneumococcal in Series $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


