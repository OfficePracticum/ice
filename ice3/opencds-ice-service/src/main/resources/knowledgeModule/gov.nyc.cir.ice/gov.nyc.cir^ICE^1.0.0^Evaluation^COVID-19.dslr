/**
 * Copyright (C) 2021 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime


//////////////////////////////////////////////////////////////////////////////////////////////////
// Evaluation of vaccines not authorized by the FDA or WHO
// If the patient has received a COVID-19 vaccine not authorized by the FDA or WHO, then evaluate the shot as Invalid/VACCINE_NOT_APPROVED_IN_US_OR_BY_WHO
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If the vaccine administered is authorized neither by the FDA nor WHO, evaluate the shot as INVALID/VACCINE_NOT_APPROVED_IN_US_OR_BY_WHO"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the vaccine administered a member of ("ICE500", "ICE501", "ICE503", "ICE504", "ICE505", "ICE506", "ICE507", "ICE508", "ICE509")
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Invalid due to "Vaccine Not Approved for Use in the U.S. or by the WHO"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////
// CVX 218, if evaluated as Invalid due to being above the maximum age for that vaccine, should be ignored for purposes of recommendations
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If CVX 218 was evaluated and Not Valid due to ABOVE_MAXIMUM_AGE_VACCINE, ignore the shot when calculating intervals"
	dialect "mvel"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	when
		There is an administered shot $administeredShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.850"
			- that has already been evaluated and whose Shot Validity Status is not VALID
			- make note of all evaluation reasons for this shot as $collectionOfStrReasons
			- the collection $collectionOfStrReasons contains "EVALUATION_REASON_CONCEPT.ABOVE_MAXIMUM_AGE_VACCINE"
			- make note of the Associated Series as $associatedTargetSeries
	then
		Mark the shot $administeredShot as Ignored
		Record that this dose rule was Processed for the TargetDose $administeredShot
		Log that this dose rule fired for the dose $administeredShot in the Series $associatedTargetSeries
end


//////////////////////////////////////////////////////////////////////////////////////////////////
// START Rules for an Additional Dose:
//  Pfizer or Moderna
//     + < 12 yrs of age, extra doses marked as ACCEPTED/EXTRA_DOSE (default)
//     + >= 12 yrs of age, extra doses marked as VALID (absolute minimum interval of 24 days enforced if in Pfizer Adult series; supplemental text if below absolute minimum interval in Adult series)    
//
//   Janssen
// If a patient completes a COVID-19 vaccine series via the Pfizer 2-dose series or Moderna 2-dose series and receives an additional shot of CVX 208, CVX 207, or CVX 213:
//     at < 28 days-4 days after series completion, then evaluate the shot as Valid SUPPLEMENTAL_TEXT; Descriptive Text: "The timing of the administration of this shot does 
//     not follow the guidelines of the regarding the minimum age and/or minimum interval."
//     at >= 28 days-4 days after series completion, then evaluate the shot as Valid.
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19(Abstract): One extra shot of FDA-approved vaccines (CVX 207, 208, 212, 213, 218) is administered for an already completed __Pfizer or Moderna Series__ and the patient >= 12 yrs of age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- the shot belongs to the Series with name a member of ("COVID19Pfizer2DoseSeries", "COVID19Moderna2DoseSeries")
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213", "ICE218")
			- make note of the date this shot was administered as $currentShotDate
			- make note of the dose number as $targetDoseNumber
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- the number of doses required to complete this Series is == $targetDoseNumber-1
		The patient information $oEvaluatedPersonAbstract must be known to complete writing this rule
			- make note of the date as $dateAt12yOfAge when the patient is "12y" of age
			- the date evalTime >= $dateAt12yOfAge
	then
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If an extra dose is administered to an already completed __Pfizer Adult Series__ prior to 24 days, mark the shot Valid and include descriptive text that follow guidelines regarding minimum ages/intervals"
		extends "COVID-19(Abstract): One extra shot of FDA-approved vaccines (CVX 207, 208, 212, 213, 218) is administered for an already completed __Pfizer or Moderna Series__ and the patient >= 12 yrs of age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- that is not the same shot as $currentShot
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that is not the same shot as $currentShot
			- that is not the same shot as $priorShot
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		// To be in the Adult series, the first dose must be >= 18y of age
		There is an administered shot $firstValidDose
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the date this shot was administered as $administrationDateOf1stDose
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is < $administrationDateOf1stDose
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $dtBirthDate
			- make note of the date as $dateAt18yOfAge when the patient is "18y" of age
			- the date $administrationDateOf1stDose >= $dateAt18yOfAge
		Confirm elapsed time between $priorShotDate and $currentShotDate < "24d"
	then
		Set the Shot Status of $currentShot to Valid
		Include Supplemental Text "The timing of the administration of this shot does not follow ACIP guidelines regarding the minimum age and/or minimum interval." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


// This rule soon to be modified to enforce absolute minimum interval for all ages
rule "COVID-19: If an extra dose is administered to an already completed __Pfizer Childhood Series__ prior to 24 days, mark the shot INVALID / BELOW_MINIMUM_INTERVAL"
		extends "COVID-19(Abstract): One extra shot of FDA-approved vaccines (CVX 207, 208, 212, 213, 218) is administered for an already completed __Pfizer or Moderna Series__ and the patient >= 12 yrs of age"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	when
		There is an administered shot $priorShot
			- the shot belongs to the series $targetSeries
			- that is not the same shot as $currentShot
			- the administration date of the shot is < $currentShotDate
			- make note of the date this shot was administered as $priorShotDate
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that is not the same shot as $currentShot
			- that is not the same shot as $priorShot
			- the administration date of the shot is > $priorShotDate
			- the administration date of the shot is < $currentShotDate
		// To be in the Childhood series, the first dose must be < 18y of age
		There is an administered shot $firstValidDose
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose Shot Validity is VALID
			- make note of the date this shot was administered as $administrationDateOf1stDose
		There does not exist an administered shot
			- the shot belongs to the series $targetSeries
			- that has already been evaluated and whose shot validity is VALID
			- the administration date of the shot is < $administrationDateOf1stDose
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the Patient's birthdate as $dtBirthDate
			- make note of the date as $dateAt18yOfAge when the patient is "18y" of age
			- the date $administrationDateOf1stDose < $dateAt18yOfAge
		Confirm elapsed time between $priorShotDate and $currentShotDate < "24d"
	then
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If one extra shot of CVX 207, 208, 212 or 213 is administered for an already completed __Janssen Series__, evaluate the shot as Valid"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "extraDoseCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Janssen1DoseSeries"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE212", "ICE213")
			- make note of the dose number as $targetDoseNumber
			- make note of the date this shot was administered as $currentShotDate
			- make note of the associated series as $associatedTargetSeries
		There is a Series $targetSeries identified by $associatedTargetSeries
			- the series is complete
			- the number of doses required to complete this Series is == $targetDoseNumber-1
	then
		Set the Shot Status of $currentShot to Valid
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

//////////////////////////////////////////////////////////////////////////////////////////////////
// END Rules for an Additional Dose in Pfizer, Moderna and Janssen Series
//////////////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////////////
// START For series representing non-FDA but authorized vaccines, mark vaccines that are not in the series as ACCEPTED/VACCINE_NOT_COUNTERED_BASED_ON_MOST_RECENT_VACCINE_GIVEN 
//////////////////////////////////////////////////////////////////////////////////////////////////

rule "COVID-19: If a different WHO-approved vaccine is administered for the AstraZeneca series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19AstraZeneca2DoseSeries"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE211", "ICE212", "ICE213", "ICE218", "ICE502", "ICE510", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end


rule "COVID-19: If a different WHO-approved vaccine is administered for the Novavax series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_SHOT_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Novavax2DoseSeries"			
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE210", "ICE212", "ICE213", "ICE218", "ICE502", "ICE510", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries	
end


rule "COVID-19: If a different WHO-approved vaccine is administered for the BIBP-Sinopharm series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_SHOT_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19BIBPSinopharm2DoseSeries"			
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE210", "ICE211", "ICE212", "ICE213", "ICE218", "ICE502", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries	
end


rule "COVID-19: If a different WHO-approved vaccine is administered for the CoronaVac-Sinovac series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_SHOT_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19CoronaVacSinovac2DoseSeries"			
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE210", "ICE211", "ICE212", "ICE213", "ICE218", "ICE502", "ICE512")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries	
end


rule "COVID-19: If a different WHO-approved vaccine is administered for the COVAXIN series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_SHOT_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19COVAXIN2DoseSeries"			
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE210", "ICE211", "ICE212", "ICE213", "ICE218", "ICE511", "ICE512")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries	
end

//////////////////////////////////////////////////////////////////////////////////////////////////
// END for series representing non-FDA but authorized vaccines, mark vaccines that are not in the series as ACCEPTED/VACCINE_NOT_COUNTERED_BASED_ON_MOST_RECENT_VACCINE_GIVEN
//////////////////////////////////////////////////////////////////////////////////////////////////


//////////////////////////////////////////////////////////////////////////////////////////////////
// START ACCEPTED vaccines in a wrong FDA-approved series
// FDA-approved vaccines in the wrong FDA-approved series that is not complete always get evaluated as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN rather than Invalid. 
// Non-FDA approved vaccines in a FDA-approved series that is not complete always gets evaluated as ACCEPTED/VACCINE_NOT_APPROVED_IN_US.
//////////////////////////////////////////////////////////////////////////////////////////////////

///////
// Janssen ACCEPTED evaluation rules
///////
rule "COVID-19: If either an FDA-approved (Pfizer, Moderna or NOS vaccine) is administered for the Janssen Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Janssen1DoseSeries"
			- the vaccine administered a member of ("ICE207", "ICE208", "ICE213", "ICE218")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "COVID-19: If a non-FDA approved but authorized vaccine is administered for the Janssen Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_APPROVED_IN_US"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Janssen1DoseSeries"
			- the vaccine administered a member of ("ICE210", "ICE211", "ICE502", "ICE510", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Approved for Use in U.S."
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
///////

///////
// Pfizer ACCEPTED evaluation rules
///////
rule "COVID-19: If the Janssen vaccine is administered for the Pfizer Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Pfizer2DoseSeries"
			- the vaccine administered is "ICE212"
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "COVID-19: If a non-FDA approved but authorized vaccine is administered for the Pfizer Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_APPROVED_IN_US"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Pfizer2DoseSeries"
			- the vaccine administered a member of ("ICE210", "ICE211", "ICE502", "ICE510", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Approved for Use in U.S."
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
///////

///////
// Moderna ACCEPTED evaluation rules
///////
rule "COVID-19: If the Janssen vaccine is administered for the Moderna Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_COUNTED_BASED_ON_MOST_RECENT_VACCINE_GIVEN"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Moderna2DoseSeries"
			- the vaccine administered is "ICE212"
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Counted Based on Most Recent Vaccine Given"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end

rule "COVID-19: If a non-FDA approved but authorized vaccine is administered for the Moderna Series and the series is not complete, evaluate it as ACCEPTED/VACCINE_NOT_APPROVED_IN_US"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "allowableVaccineCheck"
	when
		There is an administered shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Moderna2DoseSeries"
			- the vaccine administered a member of ("ICE210", "ICE211", "ICE502", "ICE510", "ICE511")
			- the series that the shot belongs to is not complete
			- make note of the associated series as $targetSeries
	then
		Include the reason for shot $currentShot Accepted due to "Vaccine Not Approved for Use in U.S."
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $targetSeries
end
///////

//////////////////////////////////////////////////////////////////////////////////////////////////
// END ACCEPTED vaccines in a wrong FDA-approved series
//////////////////////////////////////////////////////////////////////////////////////////////////


/*************************************************************************************************************************************************************************************
If the patient receives a shot below the absolute minimum age or below the absolute minimum interval, evalute the shot as Valid, with reason code SUPPLEMENTAL_TEXT: 
Descriptive Text: "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval."
/************************************************************************************************************************************************************************************/

rule "COVID-19: If shot administered below the absolute minimum age for the vaccine, override absolute vaccine minimum age check and return supplemental text that the shot does not follow guidelines for minimum age and/or minimum interval"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "minimumAgeVaccineCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850"
			- make note of the date this shot was administered as $administrationDate
			- make note of the minimum vaccine age for this shot as $absoluteMinimumVaccineAge
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the patient's birthdate as $dtBirthDate
		Confirm Elapsed time between $dtBirthDate and $administrationDate < $absoluteMinimumVaccineAge
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


// If the patient receives a shot below the absolute minimum age or below the absolute minimum interval, evalute the shot as Valid, with reason code SUPPLEMENTAL_TEXT: 
// Descriptive Text: "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval."
rule "COVID-19: If shot in the Pfizer series is administered below 17d after the prior shot and series is not complete, return supplemental text that the shot does not follow guidelines for minimum age and/or minimum interval"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Pfizer2DoseSeries"
			- the administered shot number is > 1
			- make note of the administered shot number as $administeredShotNumber
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge18y when the patient is "18y" of age
		There is an Administered Shot $priorDose
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is >= $dtDateAtAge18y
			- make note of the date this shot was administered as $administrationDatePrior
			- make note of the minimum interval from this dose to the next dose as $minimumInterval
		There does not exist an Administered Shot
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is > $administrationDatePrior
		Confirm Elapsed time between $administrationDatePrior and $administrationDate < "17d"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "COVID-19: If shot in the Moderna series is administered below 24d after the prior shot and the series is not complete, return supplemental text that the shot does not follow guidelines for minimum age and/or minimum interval"
	dialect "mvel"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseIntervalCheck"
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.850" and the Series with name "COVID19Moderna2DoseSeries"
			- the administered shot number is > 1
			- make note of the administered shot number as $administeredShotNumber
			- make note of the date this shot was administered as $administrationDate
			- make note of the associated series as $associatedTargetSeries
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dtDateAtAge18y when the patient is "18y" of age
		There is an Administered Shot $priorDose
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is >= $dtDateAtAge18y
			- make note of the date this shot was administered as $administrationDatePrior
			- make note of the minimum interval from this dose to the next dose as $minimumInterval
		There does not exist an Administered Shot
			- the shot belongs to the series $associatedTargetSeries
			- that has already been evaluated
			- the administered shot number is == $administeredShotNumber-1
			- the administration date of the shot is > $administrationDatePrior
		Confirm Elapsed time between $administrationDatePrior and $administrationDate < "24d"
	then
		Include Supplemental Text "The timing of the administration of this shot does not follow the guidelines of the regarding the minimum age and/or minimum interval." for Valid Shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
        Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

/*************************************************************************************************************************************************************************************
END If the patient receives a shot below the absolute minimum age or below the absolute minimum interval, evalute the shot as Valid, with reason code SUPPLEMENTAL_TEXT: 
/************************************************************************************************************************************************************************************/

